apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    namespace "com.otaliastudios.cameraview"

    compileSdk compileSdkVer
    buildToolsVersion = buildToolsVer

    defaultConfig {

        minSdk minSdkVer
        targetSdk targetSdkVer

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', '../proguard-debug.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', '../proguard-release.pro'
        }
    }

    // region java compile options

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    // endregion

    // region kotlin compile options

    kotlinOptions {
        jvmTarget = "17"
        allWarningsAsErrors = false
        kotlinOptions.verbose = true
        freeCompilerArgs += [
                "-Xjvm-default=all-compatibility"
        ]
    }

    // endregion

    // region lint options

    lint {
        disable 'TypographyFractions', 'TypographyQuotes', 'InvalidPackage'
        enable 'RtlHardcoded', 'RtlCompat', 'RtlEnabled'
        checkOnly 'NewApi', 'InlinedApi'
        quiet true
        abortOnError false
        ignoreWarnings true
        checkDependencies true
        ignoreTestSources true
        checkAllWarnings false
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        // DebugProbesKt.bin is used for java debugging (not needed for android)
        // Hint: https://github.com/Kotlin/kotlinx.coroutines/issues/2274
        exclude 'DebugProbesKt.bin'

        excludes += '/META-INF/{AL2.0,LGPL2.1}'
        // https://stackoverflow.com/a/61893957/1006741
        excludes -= "/META-INF/*.kotlin_module"
    }

    // endregion
}

dependencies {
    coreLibraryDesugaring desugaring.jdk

    testImplementation("junit:junit:4.13.2")
    testImplementation("org.mockito:mockito-inline:2.28.2")

    androidTestImplementation("androidx.test:runner:1.6.2")
    androidTestImplementation("androidx.test:rules:1.6.1")
    androidTestImplementation("androidx.test.ext:junit:1.2.1")
    androidTestImplementation("org.mockito:mockito-android:2.28.2")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.6.1")

    api("androidx.exifinterface:exifinterface:1.3.7")
    api("androidx.lifecycle:lifecycle-common:2.8.7")
    api("com.google.android.gms:play-services-tasks:18.2.0")
    implementation("androidx.annotation:annotation:1.9.1")
    implementation("com.otaliastudios.opengl:egloo:0.6.1")
}

if (!isJitpack) {
    apply plugin: 'com.vanniktech.maven.publish'
    mavenPublishing {
        publishToMavenCentral(com.vanniktech.maven.publish.SonatypeHost.CENTRAL_PORTAL, true)
        signAllPublications()
    }
}

if (isJitpack) {
    apply plugin: 'maven-publish'
    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = GROUP
                    artifactId = POM_ARTIFACT_ID
                    version = version
                }
            }
        }
    }
}
